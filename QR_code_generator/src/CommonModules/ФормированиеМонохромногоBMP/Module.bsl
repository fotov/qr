
Функция Сформировать(СтрокиДанных, ШиринаКартинки, УникальныйИдентификатор) Экспорт
	
	Картинка = КартинкаИзКоллекции(СтрокиДанных, ШиринаКартинки);
	
	РазмерФайла = 62 + Картинка.РазмерВБайтах; // 14 Заголовок,  40 Инфо, 8 Цвета
	Буфер = Новый БуферДвоичныхДанных(РазмерФайла, ПорядокБайтов.LittleEndian);
	// bfType
	Буфер[0] = 66; // x42
	Буфер[1] = 77; // x4D
	// bfSize
	Буфер.ЗаписатьЦелое32(2, РазмерФайла);
	// bfOffBits
	Буфер.ЗаписатьЦелое32(10, 62);
	// biSize
	Буфер.ЗаписатьЦелое32(14, 40);
	// biWidth
	Буфер.ЗаписатьЦелое32(18, Картинка.Ширина);
	// biHeight
	Буфер.ЗаписатьЦелое32(22, Картинка.Высота);
	// biPlanes
	Буфер[26] = 1;
	// biBitCount
	Буфер[28] = 1;
	// biSizeImage
	Буфер.ЗаписатьЦелое32(34, Картинка.РазмерВБайтах);
	// color table
	Буфер[54] = 255;
	Буфер[55] = 255;
	Буфер[56] = 255;
	Буфер.Записать(62, Картинка.Данные);
	
	Поток = Новый ПотокВПамяти;
	
	Поток.Записать(Буфер, 0, РазмерФайла);
	ДвоичныеДанные = Поток.ЗакрытьИПолучитьДвоичныеДанные();
	
	АдресКартинки = ПоместитьВоВременноеХранилище(ДвоичныеДанные, УникальныйИдентификатор);
	
	Возврат АдресКартинки;

КонецФункции

Функция КартинкаИзКоллекции(СтрокиДанных, ШиринаКартинки)
	
	ВысотаКартинки = СтрокиДанных.Количество();
	Картинка = ИнициализироватьКартинку(ВысотаКартинки, ШиринаКартинки);

	Для НомерСтроки = 1 По ВысотаКартинки Цикл
	
		ИндексВставки = (ВысотаКартинки - НомерСтроки) * Картинка.БайтВСтроке; // Строки в bmp идут сверху вниз
		Картинка.Данные.Записать(ИндексВставки, СтрокиДанных[НомерСтроки - 1]);
	
	КонецЦикла;
	
	Возврат Картинка;

КонецФункции

Функция ИнициализироватьКартинку(Высота, Ширина)

	Картинка = Новый Структура;
	Картинка.Вставить("Высота", Высота);
	Картинка.Вставить("Ширина", Ширина);
	Картинка.Вставить("БайтВСтроке", Цел(Ширина / 8 / 4) * 4);
	Если Ширина % 32 Тогда
		Картинка.Вставить("БайтВСтроке", Картинка.БайтВСтроке + 4);
	Иначе
		Картинка.Вставить("БайтВСтроке", Ширина / 8);
	КонецЕсли;
	Картинка.Вставить("РазмерВБайтах", Картинка.Высота * Картинка.БайтВСтроке);
	Картинка.Вставить("Данные", Новый БуферДвоичныхДанных(Картинка.РазмерВБайтах));
	
	Возврат Картинка;

КонецФункции
